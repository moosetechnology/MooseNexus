"
I am a configurable model builder.
I am used to create a Moose model for a `NexusProject`.
The model is generated by a `NexusModelCreator` that knows how to parse the sources of the project.
"
Class {
	#name : 'NexusModelBuilder',
	#superclass : 'Object',
	#instVars : [
		'project',
		'withDependencies',
		'extractor',
		'modelName',
		'modelComment'
	],
	#category : 'MooseNexus-ModelExtraction',
	#package : 'MooseNexus',
	#tag : 'ModelExtraction'
}

{ #category : 'instance creation' }
NexusModelBuilder class >> on: aNexusProject [

	^ self new project: aNexusProject
]

{ #category : 'building' }
NexusModelBuilder >> build [
	"Create the model"

	[
	| modelPath |
	self setUp.
	modelPath := self extractor run.
	self registerModel: modelPath ] ensure: [ self cleanUp ]
]

{ #category : 'cleanup' }
NexusModelBuilder >> cleanUp [
	"delete temp directories"

	(FileLocator home / project directory / 'dependencies')
		ensureDeleteAll
]

{ #category : 'accessing' }
NexusModelBuilder >> extractor [

	^ extractor ifNil: [
		  extractor := NexusModelExtractor forLanguage: project language ]
]

{ #category : 'accessing' }
NexusModelBuilder >> extractor: aNexusModelExtractor [

	extractor := aNexusModelExtractor
]

{ #category : 'initialization' }
NexusModelBuilder >> initialize [

	withDependencies := true
]

{ #category : 'accessing' }
NexusModelBuilder >> modelComment [

	^ modelComment ifNil: [ modelComment := '' ]
]

{ #category : 'accessing' }
NexusModelBuilder >> modelComment: aString [

	modelComment := aString
]

{ #category : 'accessing' }
NexusModelBuilder >> modelName [

	^ modelName ifNil: [ modelName := project name ]
]

{ #category : 'accessing' }
NexusModelBuilder >> modelName: aString [

	modelName := aString
]

{ #category : 'accessing' }
NexusModelBuilder >> project [

	^ project
]

{ #category : 'accessing' }
NexusModelBuilder >> project: aNexusProject [

	project := aNexusProject
]

{ #category : 'building' }
NexusModelBuilder >> registerModel: modelPath [
	"Name and move the model file, and register it with the project models."

	| projectDirectory source |
	projectDirectory := FileLocator home / project directory.
	source := projectDirectory / modelPath.
	source moveTo:
		((projectDirectory / 'models') ensureCreateDirectory
		 / self modelName , source extension) ensureDelete.

	project addModel: (Dictionary
			 with: 'name' -> self modelName
			 with: 'format' -> source extension
			 with: 'comment' -> self modelComment
			 with: 'timestamp' -> DateAndTime now asString
			 with: 'famixVersion' -> 'unknown')
]

{ #category : 'building' }
NexusModelBuilder >> setUp [

	| home projectDirectory dependencies |
	home := FileLocator home.
	projectDirectory := home / project directory.

	withDependencies ifTrue: [
		dependencies := (projectDirectory / 'dependencies') createDirectory.
		project dependencies do: [ :dependency |
			| artifact |
			artifact := home / (dependency at: #path).
			artifact copyTo: dependencies / artifact basename ] ].

	self extractor target: projectDirectory pathString
]

{ #category : 'accessing' }
NexusModelBuilder >> withDependencies [

	^ withDependencies
]

{ #category : 'accessing' }
NexusModelBuilder >> withDependencies: aBoolean [

	withDependencies := aBoolean
]
